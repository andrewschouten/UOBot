;==========================================================
; Script Name: Taming Mastery Trainer
; Author: Einstein
; Version: v3.1
; Client Tested with: 7.0.59.5
; EUO version tested with: 1.5.1.323
; Shard OSI
; Revision Date:  2017/03/22
; Public Release: 2017/11/05
; Public Update: 2017/04/11
; Purpose: Trains Animal Taming with mastery from 90 - 120
;==========================================================
; http://www.easyuo.com/forum/viewtopic.php?f=3&t=53314&p=432141#p432141
;==========================================================
; 90 animal taming or higher 90 animal lore (for to gain lore) or higher, Time of Legends,
; Mastery book and set Mastery to to animal taming.
; You need a pet with you, a horse or any other pet so it can train on.
; Set the skillcap, if none is set it will set it to max you can have. If you provide a password for Sige Training,
; you can eat a alacrity scroll, and it will log out and relog you when next gain is scheduled.
; It will cast combat training, wich can drag you to 120 taming. Be sure to wear high mr stuff and much lmc stuff.
; Train in a house or secure logout location if you do the blue training.
; Siege support with logout option for alacrity training
;================================================================================================================
; ** Known Issue - at server down on Siege, it will relog and go to wrong server - not find pet and log out.
;================================================================================================================
; Updates from previous revision:
;--------------------------------
; Added support for crystal ball of knowledge to show difficutly if in backpack
; Added animal lore skill to the menu - but not changes, as it is a subskill
; Major overhaul of the code and menu system
; Improved stability and meditation sub
; Added pathcheck - to veryfiy that you are on correct path of mastery
;================================================================================================================
set %halt_on_too_east #false   ;#true ;Halt if targets are too easy - if you got a ball of knowledge with you.
set %mana 40
set %meditate #true                   ; #true for to meidtate #false if you can not meditate
set %trainit anim
chooseskill %trainit real
set %skillcap 1200                         ;Set wanted Animal Taming Goal, for 100=1000 110=1100 120=1200
set %trainname Animal , #spc , Taming
if %skillcap > #skillcap
   {
   set %skillcap #skillcap
   }
if #skill < 900
   {
   display You need at least 90 Animal Taming to train mastery skills! $Halting...
   halt
   }
set %subtrainskill #true
if %subtrainskill = #true                       ;For menu if it is a subskill to main training skill
   {
   set %trainit2 anil
   chooseskill %trainint2 real
   set %skillcap2 1200                     ; Set wanted Animal Lore Cap, for 100=1000 110=1100 120=1200
   set %trainname2 Animal , #spc , Lore
   if %skillcap2 > #skillcap
      {
      set %skillcap2 #skillcap
      }
   }
;================================================================================================================
;No need for edit below here
;================================================================================================================
menu hideEUO
menu hide
finditem GYM C_ , #backpackid
if #findCnt < 1
   {
   Display ok You need a book of masteries for to do this training! Halting...
   Halt
   }
wait 10
display Target your pet to train with Taming Mastery!
set #targcurs 1
while #targcurs = 1
   {
   wait 1
   }
set %petid #ltargetid
wait 20
msg all stop$
msg all follow me$
chooseskill %trainit real
set %rot_skill #skill
set %rotwaithide #false
If #Shard = Siege , #Spc , Perilous         ;will set up siege training
   {
   set %HideWhileWorking #true
   set %bluetrain #false                                             ;No goal in use blue train on provoke - the logout is too time consuming.
   Display yesno Will you do alacrity or logout training on siege?
   if #dispRes = yes
      {
     set %bluetrain #true     ; will log out and in when time is due good for blue training on siege. Remember to fill in password
      gosub pwmenu
      }
   else
   set %bluetrain #false
   if %bluetrain = #true
      {
      set %HideWhileWorking #false
      }
   }
gosub CBKnowledge                             ;Check for ball of knowledge and activating it
set %Time_out #SCNT + 200
set %scriptname Animal , #spc , Taming , #spc , Mastery , #spc ,Trainer , #spc , v3.1
gosub showEUOMenu1
gosub initial_skills
;================================================================================================================
menu delete status
menu Font Color red
menu Text status 152 92 Checking Path....
gosub checkmastery                       ;Verify that you are on correct path
repeat
   {
   if %Time_out < #SCNT
      {
      event macro 8 5                   ; Open spellbook
      wait 1s
      event macro 9 5                   ; Close spellbook
      wait 10
      set %Time_out #SCNT + 200
      }
   if #mana < %mana
      {
      menu delete
      menu Font Color red
      menu Text status 152 92 Waiting for mana...
      gosub checkmana
      }
   if %CBK = #true                             ;Start jornalscan for difficulty on training
      {
      set %jStart #jIndex
      }
   gosub train
   if %CBK = #true                               ;Display difficulty on training
      {
      wait 20
      set %jEnd #jIndex
      for %i %jStart %jEnd
          {
          scanjournal %i
          gosub CBK_menu
          }
      }
   gosub skills
   chooseSkill %trainit real
   if #skill > %rot_skill
      {
      If #Shard = Siege , #Spc , Perilous && #skill > 699            ; Checking for ROT Timer trigger on Siege
         {
         Gosub ROT_Timer
         }
      }
   if #menubutton = Pause
      {
      gosub pause
      }
   if #menuButton = Quit
      {
      gosub stop_it
      }
   }
until #charghost = yes
menu delete status
menu Font Color red
menu Text status 152 92 You are dead...Halting!
display ok You are dead, Halting!
halt
;================================================================================================================
sub train
menu delete
menu Font Color red
menu Text status 152 92 ............Training.............
event macro 15 744                ; Cast battle train
target 3s
finditem %petid
if #findCnt < 1
   {
   menu delete
   menu Font Color red
   menu Text status 152 92 Pet is gone...halting!
   display ok Your pet is gone, script halting...
   halt
   }
SET #LTARGETKIND 1
set #ltargetid %petid
wait 10
event macro 22 0
return
;================================================================================================================
sub checkmastery
menu delete status
menu font color red
menu Text status 152 92 Checking mastery path!
set %jStart #jIndex
gosub train                          ;Doing one train for to check mastery path
wait 20
set %jEnd #jIndex
for %i %jStart %jEnd
  {
  scanjournal %i
  if You_are_not_on_the_correct in #journal
     {
      menu delete status
      menu Font Color red
      menu Text status 152 92 Wrong Mastery...Halting!
      Display ok You must change your masteries to %trainname mastery! Halting
      halt
     }
  }
return
;================================================================================================================
sub pwmenu
menu clear
menu Window Title Auto Log Out and In
menu Window Size 274 115
menu Font BgColor Black
menu Font Color White
menu text PasswordLabel 10 9 Password
menu button OKButton 178 55 86 30 Ok
menu Font BgColor Black
menu Font Color White
menu edit password 100 9 160
menu show
menu hideEUO
pwloop:
wait 1
If #MenuButton <> OKButton ;<-- Check to see if OK button has been pressed.
   {
	 Goto pwloop
	 }
menu get password
set %password #menures
Set %character 1
menu hide
menu delete
return
;================================================================================================================
sub initial_skills
chooseskill %trainit real
  set %start #skill
  set %current #skill
  str len %start
  str ins %start #dot #strRes
menu Delete start
menu font color white
menu Text start 152 8 #strRes
  str len %current
  str ins %current #dot #strRes
menu delete current
menu text current 152 24 #strRes
 set %change %current - %starting
 str len %totalgains
 str ins %totalgains #dot #strRes
menu delete change
menu Text change 172 68 + #strRes
if %subtrainskill = #true
   {
   chooseskill %trainit2 real
   set %current2 #skill
   str len %current2
   str ins %current2 #dot #strRes
   menu delete subskill
   menu Font Color gray
   menu Text subskill 152 40 #strRes
   }
return
;================================================================================================================
sub skills
chooseskill %trainit real
if %current < #skill
   {
   gosub timegain
   }
  set %current #skill
  str len %current
  str ins %current #dot #strRes
menu Font BGColor $000000
menu delete current
menu Font Color white
menu text current 152 24 #strRes
 set %change %current - %start
 str len %change
 str ins %change #dot #strRes
menu delete change
menu Font Color white
menu Text change 152 68 + #strRes
menu delete timegain
menu Font Color white
menu text timegain 204 68 %time
chooseskill %trainit
If #Skill = %skillcap
   {
   exevent skillLock %trainit locked
   menu delete status
   menu Font Color red
   menu Text status 152 92 Skillcap Reached!!!
   display You Have Reached Your %trainname Skill Cap!$ Remember to rate it on EasyUO.com Good-Bye!
   halt
   }
if %subtrainskill = #true
   {
   chooseskill %trainit2 real
   set %current2 #skill
   str len %current2
   str ins %current2 #dot #strRes
   menu delete subskill
   menu Font Color gray
   menu Text subskill 152 40 #strRes
   If #Skill = %skillcap2
          {
          exevent skillLock %trainit2 locked
          }
   }
return
;================================================================================================================
sub stop_it
menu delete status
menu Font Color red
menu Text status 152 92 Script Stopped...
Display I hope you enjoyed my Ultra Stealth Trainer!$ Remember to rate it on EasyUO.com Good-Bye!
halt
return
;================================================================================================================
sub pause
menu delete status
menu Font Color red
menu Text status 152 92 Script Paused
menu delete pause
menu Font Color Black
menu Font BGColor Red
menu Button resume 204 8 75 21 Resume
set #menubutton n/a
repeat
until #menubutton <> N/A
menu delete resume
menu Font Color WindowText
menu Font BGColor yellow
menu Button pause 204 8 75 21 Pause
menu Font Name MS Sans Serif
menu Font Style
menu Font Color white
menu Font BGColor $000000
menu delete status
menu Font Color red
menu Text status 152 92 Resuming...
Wait 5
return
;================================================================================================================
Sub CBK_menu
     if challenging in #journal
        {
        menu delete difficutly
        menu Font Color yellow
        menu Text difficulty 68 92 ..Challenging..
        }
     if too_challenging in #journal
        {
        menu delete difficutly
        menu Font Color $5959FB
        menu Text difficulty 68 92  Too Challenging
        menu delete status
        menu Font Color red
        menu Text status 152 92 Too Challanging...Halting!
        display ok Stealthing are too Challenging, wear less armor - halting!
        halt
        }
     if optimal in #journal
        {
        menu delete difficutly
        menu Font Color green
        menu Text difficulty 68 92 ....Optimal....
        }
     if easy in #journal
        {
        menu delete difficutly
        menu Font Color yellow
        menu Text difficulty 68 92 .....Easy......
        }
     if very_easy in #journal
        {
        menu delete difficutly
        menu Font Color yellow
        menu Text difficulty 68 92 ...Very Easy..
        }
     if too_easy in #journal
        {
        menu delete difficutly
        menu Font Color $5959FB
        menu Text difficulty 68 92 ...Too Easy....
        if %halt_on_too_east = #true
           {
           menu delete status
           menu Font Color red
           menu Text status 152 92 Too easy...Halting!
           display ok Stealthing are too easy - halting!$ Remember to vote on script on Easyuo.com!
           halt
           }
        }
return
;================================================================================================================
Sub CBKnowledge
Search:
finditem GMF C_ , #backpackid
if #findcnt < 1
    {
    set %CBK #false
    return
    }
wait 10
event property #FindID
if knowledge in #property
   {
   set %crystalball #FindID
   wait 10
   if turned , #spc , on in #property
      {
      set %CBK #true
      return
      }
   goto activate
   }
ignoreitem #FindID
goto search
activate:
event property %crystalball
if turned , #spc , off in #property
   {
   set #lobjectid %crystalball
   event macro 17 0
   gosub WaitForSysVars CONTSIZE = 300_150 CONTNAME = generic_gump
   set %x #contposx + 50
   set %y #contposy + 60
   click %x %y
   wait 1s
   goto activate
   }
set %CBK #true
return
;================================================================================================================
sub waitForSysVars
  set !cnt %0 / 3
  set !timeOut #scnt + 5
  if ( %0 % 3 = 1 )
    set !timeOut #scnt + % . %0
  for !i 1 !cnt
  {
    set !offset 3 * !i - 2
    set !evaluation !offset + 1
    set !value !offset + 2
    if ! ( # . % . !offset % . !evaluation % . !value )
      set !i 0
    if #scnt > !timeout
      return #false
  }
return #true
;================================================================================================================
sub showEUOMenu1
	menu Clear
	menu Font Color black
	menu Window Title %scriptname
	menu Window Color $000000
	menu Window Size 300 121
	menu Font Transparent #true
	menu Font Align Right
	menu Font Name MS Sans Serif
	menu Font Size 8
	menu Font Style
	menu Font Color white
	menu Font Transparent #false
	menu Font Align Left
	menu Font BGColor $000000
	menu Text EUOLabel1 8 8 Starting %trainname Skill
	menu Text EUOLabel2 8 24 Current %trainname Skill
	menu Font Name Comic Sans MS
	menu Text Start 152 8 000
	menu Font Color white
	menu Text current 152 24 000
  if %subtrainskill = #true
     {
     menu Font Name MS Sans Serif
	   menu Font Style
	   menu Font Color gray
     menu Text EUOLabel9 8 40 Current %trainname2 Skill
	   menu Font Name MS Sans Serif
	   menu Font Style
	   menu Font Color gray
	   menu Text subskill 152 40 000
	   }
	menu Font Name MS Sans Serif
	menu Font Style
	menu Font Color White
	menu Font BGColor $000000
	menu Text EUOLabel3 8 68 Total Gains This Session
	menu Font Name Comic Sans MS
	menu Font Color white
	menu Text change 152 68 000
	menu Font Name MS Sans Serif
	menu Font Style
	menu Font Color WindowText
	menu Font BGColor yellow
	menu Button Pause 204 8 75 21 Pause
	menu Font BGColor $5858FA
	menu Button Quit 204 32 75 21 Quit
	menu Font Style
	menu Font Color white
	menu Font BGColor $000000
	menu Text timegain 204 68 000
	menu Font Name MS Sans Serif
	menu Font Style
	menu Font Color white
	menu Font BGColor $000000
;	menu Font Name Comic Sans MS
	menu Font Color red
	menu Text EUOLabel 8 92 Status:
	menu Font Name Comic Sans MS
	menu Font Color red
	menu Text status 152 92 Initialating
if %CBK = #true
  {
	menu Font BGColor $000000
	menu Font Name Comic Sans MS
	menu Font Color white
	menu Text difficulty 68 92 Updating
	}
menu Show 421 270
menu HideEUO
return
;================================================================================================================
Sub checkmana
if #mana > %mana
   {
   return
   }
if %meditate = #false
   {
   menu delete status
   menu Font Color red
   menu Text status 152 92 Waiting for mana....
   goto wait
   }
menu delete status
menu Font Color red
menu Text status 152 92 Trying to Meditate....
set %meditating_successfully #false
repeat
   {
   if #mana = #maxmana
      {
      return
      }
   set %jStart #jIndex
   event macro 13 46
   wait 10
   set %jEnd #jIndex
   for %i %jStart %jEnd
       {
	     scanjournal %i
       if you_enter_a_meditative in #journal
          {
          set %meditating_successfully #true
          menu delete
          menu Font Color red
          menu Text status 152 92 .......Meditating........
          }
       if you_must_wait in #journal
          {
          wait 150  ; you have tried to use a skill too soon
          }
       if you_cannot_focus in #journal
          {
          wait 10s ; you just failed to meditate
          }
       }
   }
until %meditating_successfully = #true
wait:
while #mana < #maxmana
   {
   wait 1s
   }
return
;================================================================================================================
sub time
;set %AMPM AM                                                          ;Disable AM/PM
set %d1 %1 / 1 % 10
set %d2 %1 / 10 % 10
set %d3 %1 / 100 % 10
set %d4 %1 / 1000 % 10
set %d5 %1 / 10000 % 10
set %d6 %1 / 100000 % 10
set %temptime %d6 , %d5
;if %tempampm > 12 && %2 = 12                                          ;Disable AM/PM
;  {                                                                   ;Disable AM/PM
;  set %AMPM PM                                                        ;Disable AM/PM
;  set %tempampm %tempampm - 12                                        ;Disable AM/PM
;  }                                                                   ;Disable AM/PM
;if %2 = 12                                                            ;Disable AM/PM
;  return %tempampm , : , %d4 , %d3 , : , %d2 , %d1 ;, #SPC , %AMPM    ;Disable AM/PM
return %temptime , : , %d4 , %d3 , : , %d2 , %d1
return
;================================================================================================================
; %1 = ConvertDate  changes date to MM/DD/YY format
sub ConvertDate
  str Mid %1 3 2
  set %tempstr #STRRES , /
  str Mid %1 5 2
  set %tempstr %tempstr , #STRRES , /
  str Mid %1 1 2
  set %tempstr %tempstr , #STRRES
return %tempstr
;================================================================================================================
sub timegain
gosub time #time 12
set %time #RESULT
gosub ConvertDate #DATE
set %date #RESULT
return
;================================================================================================================
sub hide
menu delete status
menu Font Color red
menu Text status 152 92 Hiding...
If H notin #CharStatus
   {
   repeat
   Event Macro 13 21       ; Hide
   if H in #charstatus
      {
      wait 5
      goto continue
      }
   Wait %waithide
   until H in #charstatus
   }
continue:
if %rotwaithide = #true
   {
   menu delete status
   menu Font Color red
   menu Text status 152 92 Waiting for RoT...
   }
return
;================================================================================================================
Sub RoT_Timer       ; Sets the RoT timer
menu delete status
menu Font Color red
menu Text status 152 92 Waiting for RoT...
gosub skills
Chooseskill %trainit Real
Set %rot_skill #skill
set %rotwaithide #true
If #Skill < 800                        ; 5 Minute Interval
   {
   Set %ROTSkillTimerT #SCNT + 300
   }
If #Skill >= 800 && #Skill < 900       ; 8 Minute Interval
   {
   Set %ROTSkillTimerT #SCNT + 480
   {
If #Skill >= 900 && #Skill < 1000      ; 12 Minute Interval
   {
   Set %ROTSkillTimerT #SCNT + 720
   }
If #Skill >= 1000 && #Skill < 1200     ; 15 Minute Interval
   {
   Set %ROTSkillTimerT #SCNT + 900
   }
if %bluetrain = #false
   {
   set %OldTime #Time
   }
If %HideWhileWorking = #True
   {
   gosub hide
   }
if %bluetrain = #true
   {
   gosub logout
   }
Repeat

If %ROTSkillTimerT - #SCNT > 120
   {
   IF %HideWhileWorking = #True
      {
      If H notin #CharStatus
         {
         if %bluetrain = #false
            {
            gosub hide       ; Hide
            }
         }
   if %bluetrain = #false
      {
      If #Time - %OldTIme > 500            ; Time for to prevent timeout
         {
         event macro 8 5                   ; Open spellbook
         wait 1s
         event macro 9 5
         wait 10
         set %OldTime #Time
         }
      }
   Wait 50
   }
Until #SCNT >= %ROTSkillTimerT
if %bluetrain = #true
      {
      gosub loginagain
      wait 3s
      even macro 8 7
      wait 1s
      }
Until #SCNT >= %ROTSkillTimerT
set %rotwaithide #false
return
;================================================================================================================
sub logout
menu Font Color red
menu delete status
menu Text status 152 92 Logging out
namespace push
namespace local logout
logout:
set !timeOut #scnt + 5
event macro 8 1
wait 20
waitpaperdoll:
if #scnt > !timeOut
   {
	 goto logout
	 }
if #contname <> paperdoll_gump
   {
	 goto waitpaperdoll
	 }
contpos #clixres 0
wait 20
_waitConfirm:
set !timeOut #scnt + 3
set !paperDollX #clixres + 210
click !paperDollX 110
waitlogout:
if #scnt > !timeOut
   {
	 goto _waitConfirm
   }
if #contname <> YesNo_gump
   {
	 goto waitlogout
	 }
set !x #contposx + 120
set !y #contposy + 90
_quit:
click !x !y
set !timeOut #scnt + 20
_waitlogout:
if #scnt > !timeout
   {
	 goto _quit
   }
if #contname <> MainMenu_gump
   {
	 goto _waitlogout
	 }
namespace pop
menu delete status
menu Font Color red
menu Text status 152 92 Waiting for RoT
return
;================================================================================================================
sub loginagain
if #contName = mainmenu_gump && #contKind = KRDD
   {
   _Login:
   gosub EnterPassword
   gosub PressNextButton
   gosub WaitForGump normal_gump CHCD 60
   if #contName = waiting_gump && #contKind = SRHB
      {
      click 320 350 f dmc
      gosub WaitForGump normal_gump UYLB
      goto _Login
      }
    gosub PressNextButton
    gosub WaitForGump normal_gump UYLB 30
    gosub SelectChar
    gosub PressNextButton
    gosub WaitForLogin
    }
return
;================================================================================================================
sub PressNextButton
click 615 445 f dmc ; Next Button
return
;================================================================================================================
sub EnterPassword
click 510 400 f dmc ; Password
gosub ClearPlace
msg %password
return
;================================================================================================================
sub ClearPlace
for %i 1 16
    {
    key BACK
    }
return
;================================================================================================================
sub SelectChar
set %cx_ 360
set %cy_ 100 + ( %Character * 40 )
click %cx_ %cy_ f dmc
gosub WaitForGump login_gump AAWB 10
return
;================================================================================================================
sub WaitForGump
set %contName %1
set %contKind %2
set %timeCheck #false
set %timeOut #scnt
if %0 = 3
   {
   set %timeCheck #true
   set %timeOut #scnt + %3
   }
while #contName <> %contName || #contKind <> %contKind
    {
    if %timeCheck && #scnt > %timeOut
    break
    }
return
;================================================================================================================
sub WaitForLogin
set %timeOut #scnt + 60
while #CliLogged = 0 && #scnt < %timeOut
wait
return
;================================================================================================================


