;==================================
; Script Name: Cooking 1.0
; Author: Athelstan - 166467051 - oceans@gamesnet.it
; Version: 1.0
; Client Tested with: 6.0.1.3
; EUO version tested with: 1.50
; Shard OSI / FS:  UODreams.it
; Revision Date: 12/10/07
; Public Release: 12/09/07
; Purpose: Script Cooking
;Go your home, stay near a stove, secure a chest
;In the chest put in 70-80 skillet and
;6000-7000 cut of Raw Ribs, open the chest and
;play the script. 0-100 cooking
;I advise to train the skill cooking from a baker
;www.sweetuo.com
;==================================
;Select the chest
Set %Chest QFKYFVD ;Change here
;==================================
;Don't edit now
Set %Skillet OGG ; DND  ; ogg
Set %rawFood LLI
Set %cookedFood WLI
      
Loop:

GoSub Skill
GoSub RestockSkillet
GoSub RestockFood
GoSub Cook
GoSub Wei

Goto Loop

;==================================
Sub Cook
{
    finditem %Skillet C_ , #BACKPACKID
    wait 10
    Set #LOBJECTID #FINDID
    event macro 17
    wait 15
    click 80 200 dmc ; barbeque
    wait 15
   ; click 284 200 dmc ; lamb
    click 280 141 dmc ; chiken leg
    wait 15
    return
}
;==================================
Sub Wei
{
    If #WEIGHT > #MAXWEIGHT
    {
      finditem %cookedFood C_ , #BACKPACKID
      wait 10
      Exevent Drag #FINDID #FINDSTACK
      wait 10
     set %y #charposy + 1
      Exevent Dropg #charposx %y #charposz
      wait 10
    }
    return
}
;==================================
Sub RestockFood
{
   finditem %rawFood C_ , #BACKPACKID
   wait 10
   if #FINDCNT = 0
   {
     finditem %rawFood C_ , %Chest
     wait 10
     If #FINDID = 0
        {
        Display The Cuts of Raw Ribs are finish
        halt
        }
     Exevent Drag #FINDID 1
	   wait 10
	   Exevent Dropc #BACKPACKID
	   wait 10
   }
   return
}
;==================================
Sub RestockSkillet
{
   finditem %Skillet C_ , #BACKPACKID
   wait 10
   if #FINDCNT = 0
   {
      finditem %Skillet C_ , %Chest
      wait 10
      If #FINDID = 0
         {
         Display The Skillets are finish
         halt
         }
      Exevent Drag #FINDID
      wait 10
      Exevent Dropc #BACKPACKID
      wait 10
   }
   return
}
;==================================
Sub Skill
{
    chooseskill Cook
    if #skill = 1000
       {
       display Congratulation You finish
       halt
       }
    return
}
